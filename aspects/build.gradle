buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "org.aspectj:aspectjtools:1.8.13"
  }
}

dependencies { 
  compile 'org.aspectj:aspectjrt:1.8.13'
}

apply plugin: 'findbugs'

findbugs {
  ignoreFailures = true
  effort = "max"
  showProgress = true
  reportLevel = "high"
  showProgress = false
}

tasks.withType(FindBugs) {
  reports {
    xml.enabled = false
    html.enabled = true
  }
}
test {
  jacoco {
      enabled = false // JaCoCo doesn't like compile time weaving.
  }
}


tasks.withType(JavaCompile) {
  sourceCompatibility = 1.7
  targetCompatibility = 1.7
}

compileTestJava {
  doLast{
    // gradle warning if `classesDir` is used, but this project only has one dir, so this is admissable.
    String classesDestDir = sourceSets.test.output.classesDirs.collect{it}.head().absolutePath
    
    ant.taskdef(
      resource:"org/aspectj/tools/ant/taskdefs/aspectjTaskdefs.properties",
      classpath: project.buildscript.configurations.classpath.asPath
    )
    ant.iajc(
      source:"1.7",
      target:"1.7",
      destDir: classesDestDir,
      maxmem:"512m",
      fork:"true",
      aspectPath:configurations.testCompile.asPath,
      classpath:configurations.testRuntime.asPath
    ){
      sourceroots{
        sourceSets.main.java.srcDirs.each{
          pathelement(location:it.absolutePath)
        }
        sourceSets.test.java.srcDirs.each{
          pathelement(location:it.absolutePath)
        }
      }
    }
  }
}
